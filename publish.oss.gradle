apply plugin: "maven-publish"
publishing {
    repositories {
        maven {
            name "oss"
            def ossSnapshotRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            def ossStagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = project.hasProperty("release") ? ossStagingRepoUrl : ossSnapshotRepoUrl
            credentials {
                username property("maven2.username")
                password property("maven2.password")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            if (project.hasProperty("release")) {
                version = "$projectVersion"
            } else {
                version = "$projectVersion-SNAPSHOT"
            }

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/YuanWenqing/shopify4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yuanwq'
                        name = 'YuanWenqing'
                        email = 'ywq8876@163.com'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com:YuanWenqing/{$rootProject.name}.git"
                    developerConnection = 'scm:git:ssh://github.com:YuanWenqing/{$rootProject.name}.git'
                    url = 'https://github.com/YuanWenqing/{$rootProject.name}'
                }
            }
        }
    }
}
publish {
    doLast {
        def pub = publishing.publications.mavenJava
        println "artifact: ${pub.groupId}:${pub.artifactId}:${pub.version}"
        for (repo in publishing.repositories) {
            println "upload to ${repo.url}"
        }
    }
}
publishToMavenLocal {
    doLast {
        def pub = publishing.publications.mavenJava
        println "artifact: ${pub.groupId}:${pub.artifactId}:${pub.version}"
    }
}
task('local').dependsOn(publishToMavenLocal)

apply plugin: "signing"  // generate a signature file for each artifact
signing {
    sign publishing.publications.mavenJava
}
